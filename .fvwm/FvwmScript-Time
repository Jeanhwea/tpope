# ~/.fvwm/FvwmScript-Time
# $Id$

WindowTitle {FvwmScript-Status}
WindowSize 112 16
BackColor {PaleTurquoise}
#ShadowColor {PaleTurquoise}
#HilightColor {PaleTurquoise}
ForeColor {Black}

# the following should be commented out if the Calendar is used outside
# of FvwmButtons
WindowPosition 30000 30000

Init
 Begin
  Set $font =(GetOutput {exec echo "$fvwm_datefont"} 1 -1)
  ChangeFont 1 $font
  ChangeFont 3 $font
  Set $font =(GetOutput {exec echo "$fvwm_timefont"} 1 -1)
  ChangeFont 2 $font
  Set $date =(GetOutput {exec date '+%Y-%m-%d'} 1 -1)
  Set $time =(GetOutput {exec date '+%R'} 1 -1)
  If (RemainderOfDiv (GetTime) 60)<10 Then
   Set $seconds ={:0} (RemainderOfDiv (GetTime) 60)
  Else
   Set $seconds ={:} (RemainderOfDiv (GetTime) 60)
  ChangeTitle 1 $date
  ChangeTitle 2 $time
 End

PeriodicTasks
 Begin
  If (RemainderOfDiv (GetTime) 60)==0 Then
   Set $time =(GetOutput {exec date '+%R'} 1 -1)
  If (RemainderOfDiv (GetTime) 3600)==0 Then
   Set $date =(GetOutput {exec date '+%Y-%m-%d'} 1 -1)
  If (RemainderOfDiv (GetTime) 60)<10 Then
   Set $seconds ={:0} (RemainderOfDiv (GetTime) 60)
  Else
   Set $seconds ={:} (RemainderOfDiv (GetTime) 60)
  ChangeTitle 3 $seconds
  If (RemainderOfDiv (GetTime) 60)==0 Then
  ChangeTitle 2 $time
  If (RemainderOfDiv (GetTime) 3600)==0 Then
  ChangeTitle 1 $date
 End

Widget 1
Property
 Position 0 0
 Size 68 16
 Type ItemDraw
 Flags Center NoReliefString
 Title {YYYY-mm-dd}
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 2
Property
 Position 66 0
 Size 45 16
 Type ItemDraw
 Flags Right
 Title {HH:MM}
Main
 Case message of
  SingleClic :
  Begin
  End
End

Widget 3
Property
 Position 116 0
 Size 20 16
 Type ItemDraw
 Flags Left NoReliefString
 Title {:SS}
Main
 Case message of
  SingleClic :
  Begin
  End
End

