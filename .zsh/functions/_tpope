#compdef tpope launch x-terminal-emulator xauth-get sursync sync-tpope

local tmp

case $service in
tpope)
if [[ $CURRENT = 2 ]]; then
    tmp=(start stop restart reload force-reload 'runcontrol:start or stop daemons' rclink 'launch:start and disown a process' 'sensible-browser:web browser' 'x-terminal-emulator: X terminal emulator' 'xauth-get:retrieve X authoritization' 'ruptime:remote uptime' 'rwho: remote who')
    _describe 'tpope command' tmp -- && ret=0
else
    shift words
    (( CURRENT-- ))
    curcontext="${curcontext%:*:*}:tpope-${words[1]}:"
    [[ $words[1] = sync ]] && words[1]=sync-tpope
    [[ $words[1] = link ]] && words[1]=rclink
    [[ $words[1] = sursync ]] && words[1]=rsync
    _normal
fi ;;
x-terminal-emulator)
    words[1]=xterm
    _normal ;;
xauth-get)
    _hosts ;;
sync-tpope)
    case $words[CURRENT-1] in
    -a) _tar_archive cf ;;
    -z) _tar_archive cz ;;
    -j) _tar_archive cj ;;
    *)
    _arguments \
	'-g[following should get GUI files]' \
	'-G[following should not get GUI files]' \
	'-t[following hosts are trusted]' \
	'-T[following hosts are not trusted]' \
	'-s[add simpson hosts]' \
	'-a[archive to tar]' \
	'-z[archive to tar.gz]' \
	'-j[archive to tar.bz2]' \
	'-A[pipe over ssh]' \
	'-Z[pipe gzipped over ssh]' \
	':host:_hosts'
	;;
    esac
    ;;
esac
